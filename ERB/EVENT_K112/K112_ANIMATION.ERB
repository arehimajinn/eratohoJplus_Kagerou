;----------------------------------------------------------------
;K112_ANIMATION.ERB
;アニメーション関係の関数をぶち込んである。
;----------------------------------------------------------------
@OPENING_ANIMATION_K112(ENDING_FLAG = 0)
#DIM ENDING_FLAG
#DIM MOON
#DIM MOON_LOOP
#DIM UNDER
#DIM PRINT_LOOP
#DIM WAIT_LOOP

CALL SAVELINE
REDRAW 0
;まず邪魔なのを画面の外に
CALL NEWLINE(150)

;月の移り変わりの表示
MOON = 0
FOR MOON_LOOP, 0, 29
	SETCOLOR GETDEFCOLOR()
	PRINTFORM %" " * CENTERLINE("あ")%
	SELECTCASE MOON
		CASE 0
			SETCOLOR 0xFFFF00
			PRINTL 🌑
		CASE 1
			PRINTL 🌒
		CASE 2
			PRINTL 🌓
		CASE 3
			PRINTL 🌔
		CASE 4
			PRINTL 🌕
		CASE 5
			PRINTL 🌖
		CASE 6
			PRINTL 🌗
		CASE 7
			PRINTL 🌘
	ENDSELECT
	;繰り返した回数分だけ改行（上限13行）
	CALL NEWLINE(MIN(MOON_LOOP + 1, 13))
	MOON++
	SIF MOON == 8
		MOON = 0
	TWAIT 300, 1
	;満月時に一旦ストップ
	SIF MOON_LOOP == 16
		TWAIT 1500, 1
	;最後以外は表示を一回一回消す
	SIF MOON_LOOP != 28
		CLEARLINE MIN(MOON_LOOP + 1, 14)
NEXT

;7行上に
FOR WAIT_LOOP, 0, 7
	PRINTL
	TWAIT 150, 1
NEXT
;月と下三行を残す
CLEARLINE 17
;The episode ofの表示。タイピングばりに。
FOR PRINT_LOOP, 0, STRLENS("The episode of") + 1
	;_をちらちらさせて入力してるっぽく
	FOR UNDER, 0, 2
		PRINTFORML %" " * CENTERLINE("The episode of　　　　　　　") + SUBSTRING("The episode of", 0, PRINT_LOOP)%\@ UNDER ?  # _ \@
		CALL NEWLINE(16)
		TWAIT 100, 1
		CLEARLINE 17
	NEXT
NEXT
PRINTFORML %" " * CENTERLINE("The episode of　　　　　　　") + "The episode of"%

;"lonely werewolf"の表示処理
;大体上と一緒
FOR PRINT_LOOP, 0, STRLENS("\"lonely werewolf\"") + 1
	FOR UNDER, 0, 2
		PRINTFORML %" " * CENTERLINE("\"lonely werewolf\"") + "　　" + SUBSTRING("\"lonely werewolf\"", 0, PRINT_LOOP)%\@ UNDER ? _ #  \@
		CALL NEWLINE(15)
		TWAIT 100, 1
		CLEARLINE 16
	NEXT
NEXT
;上に表示する
PRINTFORML %" " * CENTERLINE("\"lonely werewolf\"") + "　　" + "\"lonely werewolf\""%
CALL NEWLINE(15)
TWAIT 1000, 1

;一回消してフェードアウト処理で表示しながら消す。
CLEARLINE 21
CALL FADEOUTLINE(15, 8, " " * CENTERLINE("あ") + "🌕", "", "", "", " " * CENTERLINE("The episode of　　　　　　　") + "The episode of　　　　　　　", " " * CENTERLINE("\"lonely werewolf\"") + "　　" + "\"lonely werewolf\"")
CLEARLINE 21
;エンディングから呼び出されていなければ終了処理
IF !ENDING_FLAG
	REDRAW 1
	CALL RETURNLINE
	RETURN RESULT
ENDIF
ALIGNMENT CENTER
;これは古いバージョンなので更新します的な表示
CALL FADEINLINE(13, 8, "This is an old version. Update")
TWAIT 100, 1
CLEARLINE 14
;...の表示。
FOR PRINT_LOOP, 0, 3
	PRINTFORML This is an old version. Update%"." * (1 + PRINT_LOOP)%
	CALL NEWLINE(13)
	TWAIT 500, 1
	CLEARLINE 14
NEXT

CALL NEWLINE(13)

;更新終わりのフェードアウト
CALL FADEOUTLINE(13, 8, "This is an old version. Update...")
ALIGNMENT LEFT

;月とThe episode of "lonely werewolf"の表示
CALL FADEINLINE(15, 8, " " * CENTERLINE("あ") + "🌕", "", "", "", " " * CENTERLINE("The episode of　　　　　　　") + "The episode of　　　　　　　", " " * CENTERLINE("\"lonely werewolf\"") + "　　" + "\"lonely werewolf\"")
;消す
CLEARLINE 16

;"lonely werewolf"を"werewolf and you!"分消す。そしてしばらく点滅させる
FOR PRINT_LOOP, 0, STRLENS("　　" + "\"werewolf and you!\"") - 1
	FOR UNDER, 0, 2
		PRINTFORML %SUBSTRING(" " * CENTERLINE("\"lonely werewolf\"") + "　　" + "\"lonely werewolf\"", 0, STRLENS(" " * CENTERLINE("\"lonely werewolf\"") + "　　" + "\"lonely werewolf\"") - MIN(PRINT_LOOP, STRLENS("　　" + "\"werewolf and you!\"") - 6))%\@ UNDER ? _ #  \@
		CALL NEWLINE(15)
		TWAIT 150, 1
		CLEARLINE 16
	NEXT
NEXT

;"werewolf and you!"の表示
FOR PRINT_LOOP, 0, STRLENS("　　" + "\"werewolf and you!\"")
	FOR UNDER, 0, 2
		PRINTFORML %" " * CENTERLINE("\"lonely werewolf\"") + "　　" + SUBSTRING("\"werewolf and you!\"", 0, PRINT_LOOP)%\@ UNDER ? _ #  \@
		CALL NEWLINE(15)
		TWAIT 200, 1
		CLEARLINE 16
	NEXT
NEXT
PRINTFORML %" " * CENTERLINE("\"lonely werewolf\"") + "　　" + "\"werewolf and you!\""%
CALL NEWLINE(15)

;月を動かす前のWAITと画面をまっさらにする処理
TWAIT 1300, 1
CLEARLINE 21

;月を動かす
FOR MOON_LOOP, 0, 5
	CLEARLINE !MOON_LOOP ? 0 # 21
	PRINTFORM %" " * CENTERLINE("あ")%
	SELECTCASE MOON_LOOP
		CASE 0
			PRINTL 🌕
		CASE 1
			PRINTL 🌖
		CASE 2
			PRINTL 🌗
		CASE 3
			PRINTL 🌘
		CASE 4
			SETCOLOR 0xFFFF00
			PRINTL 🌑
	ENDSELECT
	CALL NEWLINE(3)
	PRINTFORML %" " * CENTERLINE("The episode of　　　　　　　") + "The episode of"%
	PRINTFORML %" " * CENTERLINE("\"lonely werewolf\"") + "　　" + "\"werewolf and you!\""%
	CALL NEWLINE(15)
	TWAIT 500, 1
NEXT
TWAIT 2000, 1
;すべて消す
REDRAW 1
CLEARLINE 21
;すべてフェードアウト
CALL FADEOUTLINE(15, 8, " " * CENTERLINE("🌑") + "🌑", "", "", "", " " * CENTERLINE("The episode of　　　　　　　") + "The episode of　　　　　　　", " " * CENTERLINE("\"lonely werewolf\"") + "　　" + "\"lonely werewolf\"")
;元に戻す
SETCOLOR GETDEFCOLOR()
CALL RETURNLINE
CLEARLINE 150
RETURN RESULT

;----------------------------------------------------------------
;ENDING_ANIMATION_K112
;エンディングの面倒な処理をまとめた
;----------------------------------------------------------------
@ENDING_ANIMATION_K112
#DIM COLOR
#DIM UNDER
#DIM NEWLINE_VALUE
#DIM STAFF_ROLL
#DIMS TEXT, 40

;描画のちらつきカット
REDRAW 0
;元に戻すためにここで
CALL SAVELINE
;スタッフロール開始
CALL CENTERCUTIN(13, "―― True Ending 「孤独？なウェアウルフ」")
SETCOLOR 0xFFFF00
CALL CENTERCUTIN(13, "原作：ＺＵＮ", "　『東方Projectシリーズ』")
SETCOLOR GETDEFCOLOR()
CALL NEWLINE(150)

;スタッフロール（一覧）
FOR STAFF_ROLL, 0, 50
	SELECTCASE STAFF_ROLL
		CASE 0
			ALIGNMENT CENTER
			FONTBOLD
			PRINTFORM 参考にした口上（敬称略）
		CASE 1
			TEXT '= "eratohoJ+用フランドール（妹様）口上"
		CASE 2
			TEXT '= "eratohoJ+用アリス口上"
		CASE 3
			TEXT '= "eratohoJ+用 リリーブラック口上"
		CASE 4
			TEXT '= "eratohoJ用霊夢口上 ver2"
		CASE 5
			TEXT '= "J魔理沙口上"
		CASE 6
			TEXT '= "その他、多くの口上作者の皆様"
		CASE 8
			FONTBOLD
			PRINTFORM スペシャルサンクス
		CASE 9
			TEXT '= "うふふ板影狼スレ"
		CASE 10
			TEXT '= "eraIRCの皆様"
		CASE 11
			TEXT '= "名称不明の修正してくれた人"
	ENDSELECT
	FONTREGULAR
	PRINTFORML %TEXT%
	TEXT '= ""
	TWAIT 200, 1
NEXT
CLEARLINE 50
ALIGNMENT LEFT

;謝辞。カラー付きで
FOR STAFF_ROLL, 0, 6
	SELECTCASE STAFF_ROLL
		CASE 0
			;明るい赤
			SETCOLOR 0xFF8080
			TEXT:0 '= "関係者皆様への謝辞"
			NEWLINE_VALUE = 9
		CASE 1
			;イメージカラー：Indianred/まんまかもしれないけど赤に黒を混ぜたようなイメージ。姉のほうはScarlet-Redでそのまんまなのでそっくりなのかも
			;Darkredでも良かったんだけど見栄えが悪い。あとぱっと見正常に見えるしこっちで
			SETCOLOR 0xCD5C5C
			TEXT:0 '= "　　　　eratohoJ+用フランドール（妹様）口上作者様　　　　"
			TEXT:2 '= "数々の参考資料や考え方を（勝手に）いただいたり、"
			TEXT:3 '= "この口上の影狼のイメージの一部は影響を（勝手に）受けていたりします。"
			TEXT:4 '= "本当にありがとうございました！"
			NEWLINE_VALUE = 11
		CASE 2
			;イメージカラー：lincoln yellow/都会派っぽい色。
			SETCOLOR 0xFFE60F
			TEXT:0 '= "eratohoJ+用アリス口上作者様"
			TEXT:2 '= "一部のシステムはアリス口上からの流用＆改変で出来ています。"
			TEXT:3 '= "本当にありがとうございました！"
		CASE 3
			;イメージカラー：Azurite/藍色（0x165E83）は見栄えがやっぱり悪いのでアズライト辺りで。
			SETCOLOR 0x0025B0
			TEXT:0 '= "toroiyaさん"
			TEXT:2 '= "この口上の一部の分岐は彼（彼女？）のアイデアで出来ています"
			TEXT:3 '= "ありがとうございました！"
		CASE 4
			;イメージカラー：選択中の色/やっぱり開発者さんはそういう本体に関係した色がいいかなって
			SETCOLOR GETFOCUSCOLOR()
			TEXT:0 '= "まだ名無し様"
			TEXT:2 '= "あなたが居なければJ+が生まれることもなかったでしょう"
			TEXT:3 '= "J+の元となった素晴らしいバリアントを生み出してくれて、ありがとうございました！"
		CASE 5
			;イメージカラー：デフォルト/上に同じく
			SETCOLOR GETDEFCOLOR()
			TEXT:0 '= "名無し様"
			TEXT:2 '= "拙いところを直してくださったり、要望を聞き入れてくださったり、"
			TEXT:3 '= "色々とありがとうございました！"
			TEXT:4 '= "そしてこのような素晴らしいバリアントを作っていただき、"
			TEXT:5 '= "本当にありがとうございました！"
	ENDSELECT
	CALL CENTERCUTIN(NEWLINE_VALUE, TEXT:0, TEXT:1, TEXT:2, TEXT:3, TEXT:4, TEXT:5)
	TEXT:0 '= "", " ", "", "", "", ""
NEXT

;プレイしてくれてありがとう！　の表示。無駄に七色
FOR STAFF_ROLL, 0, STRLENSU("Thank you for playing!") + 16
	SIF STRLENSU("Thank you for playing!") == STAFF_ROLL - 1
		COLOR = 0
	COLOR++
	;赤・橙・黄・緑・青・藍・紫。ゴリ押し
	SELECTCASE COLOR
		CASE 1, 13
			SETCOLOR 0xFF0000
		CASE 2, 12, 14
			SETCOLOR 0xFF8000
		CASE 3, 11, 15
			SETCOLOR 0xFFFF00
		CASE 4, 10, 16
			SETCOLOR 0x009900
		CASE 5, 9, 17
			SETCOLOR 0x0000FF
		CASE 6, 8, 18
			SETCOLOR 0x0000CC
		CASE 7, 19
			SETCOLOR 0x6600CC
	ENDSELECT
	IF STRLENSU("Thank you for playing!") >= STAFF_ROLL + 1
		FOR UNDER, 0, 2
			PRINTFORML %" " * CENTERLINE("Thank you for playing!") + SUBSTRING("Thank you for playing!", 0, STAFF_ROLL)%\@ UNDER ?  # _ \@
			CALL NEWLINE(13)
			TWAIT 150, 1
			CLEARLINE 14
		NEXT
	ELSE
		PRINTFORML %" " * CENTERLINE("Thank you for playing!") + SUBSTRING("Thank you for playing!", 0, -1)%
		CALL NEWLINE(13)
		TWAIT 300, 1
		CLEARLINE 14
	ENDIF
NEXT
;プレイしてくれてありがとう！　のフェードアウト
CALL FADEOUTLINE(13, 8, " " * CENTERLINE("Thank you for playing!") + "Thank you for playing!")
TWAIT 500, 1
CLEARLINE 14
;作者名の表示
CALL CENTERCUTIN(13, "作者：　　", "暇人")
SETCOLOR GETDEFCOLOR()
CALL RETURNLINE
CALL OPENING_ANIMATION_K112(1)
RETURN RESULT

